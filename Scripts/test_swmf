#!/bin/csh

# This script checks out the latest version of the SWMF, 
# executes the SWMF tests and sends the results by e-mail. 
# This script is meant to be used by the SWMF developers. 
# Most users should execute the SWMF test by simply typing
#
# make test 
#
# in the main directory of an installed SWMF distribution.
#
# The script is written to be fairly general, but some 
# customization may be necessary. 
#
# You can place this script anywhere, e.g. in your home directory, 
# your personal bin/ directory, or keep it in SWMF/Scripts/ 
# (but then it should not be modified so that CVS remains happy). 
#
# The script can be executed by simply typing 
#
# ./test_swmf
#
# To run the SWMF tests perodically, you can use the crontab facility.
# Type 'crontab -e' to add a new entry to your crontab.
# Here is an example entry for nightly runs at 2:30 am:
#
# MAILTO=""
# 30 02 * * * $HOME/bin/test_swmf
#
# Here is an example entry for weekly runs on Sundays at 1:00 am:
#
# MAILTO=""
# 00 01 * * 0 SWMF/Scripts/test_swmf
#
# The MAILTO="" tells crontab not to send an automated e-mail.

# The script starts here

# Execute systemwide startup files (may or may not be necessary)
source /etc/csh.cshrc

# Go to your home directory
cd $HOME

# execute local startup file (may or may not be necessary)
source .cshrc

# update SWMF_data and CRASH_data/input (no need if /csem1 is mounted)
cvs update -d SWMF_data CRASH_data/input

# Create a temporary directory for the tests
mkdir -p Tmp
cd Tmp

# Remove the previous test directory if necessary
rm -rf SWMF

# Checkout and install the latest code together with HYPRE
cvs co -D "`date +%m/%d/%Y` 19:00" SWMF BATL HYPRE >& /dev/null
mv BATL SWMF/
mv HYPRE SWMF/util
cd SWMF
./Config.pl -install -compiler=gfortran -hypre >& test_swmf.log
cd BATL
./Config.pl -install -compiler=gfortran >>& ../test_swmf.log
cd ..

#
# Directory on herot. The date is set when the test starts!
#
setenv NEWTESTDIR Sites/SWMF_TEST_RESULTS/`date +%Y/%m/%d`/MACHINENAME

#
# Select one of the following options:
#
#make -j test MPIRUN= >>& test_swmf.log # Compile parallel but run on 1 processor
#make -j test >>& test_swmf.log #	  Run the tests on 2 processors
#make -j test NP=4 >>& test_swmf.log #	  Run the tests on 4 processors

# Run with aprun -q (quiet) on 8 cores, serial tests are run with aprun -n 1
make -j test PARALLEL='aprun -q' NP=8 SERIAL='aprun -q -n 1' 

#
# Send back the test results by scp
#
ssh herot.engin.umich.edu mkdir -p ${NEWTESTDIR}
scp test_swmf.res test_swmf.log herot.engin.umich.edu:${NEWTESTDIR}
